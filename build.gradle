buildscript {
    repositories {
        jcenter()
    }
}

plugins {
  id "java"
  id "com.jfrog.artifactory" version "4.5.4"
  id "com.jfrog.bintray" version "1.7.3"
  id "maven-publish"
}

repositories {
    jcenter()
}

// run gradle with "-Drealease=true" to *NOT* automatically append "-SNAPSHOT" to the version
version = '1.0' + (!Boolean.valueOf(System.getProperty("release")) ? "-SNAPSHOT" : "")
sourceCompatibility = 1.8

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

dependencies {
    compile('org.slf4j:slf4j-api:1.7.25')
    testCompile('org.slf4j:slf4j-log4j12:1.7.25')
    testCompile('org.testng:testng:6.8.7')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jabarkarim"
            name "Jabar Karim"
            email "jabar@pydio.com"
        }
        developer {
            id "bsinou"
            name "Bruno Sinou"
            email "bruno.sinou@posteo.de"
        }
    }

    scm {
        url "https://github.com/bsinou/pydio-sdk-java"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.pydio.sdk'
            artifactId 'pydio-sdk-java'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java SDK for Pydio Cells (and legacy Pydio 8)')
                root.appendNode('name', 'pydio-sdk-java')
                root.appendNode('url', 'https://github.com/bsinou/pydio-sdk-java')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'pydio-maven-releases'
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
            properties = [
                    'build.number': buildNumber,
                    'build.name': 'pydio-sdk-java'
            ]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName('pydio-sdk-java')
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        repo = 'pydio-maven-releases'
        name = 'pydio-sdk-java'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bsinou/cells-sdk-java'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
            gpg {
                sign = true
            }
        }
    }

    publish = true
}
